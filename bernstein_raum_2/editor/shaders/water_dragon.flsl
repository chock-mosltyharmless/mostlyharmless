#version 120

const int numCenters = 16;
varying vec3 centers[numCenters];

varying vec4 color;
varying vec3 ppos;
varying vec3 location;
varying vec3 normal;
uniform sampler2D BGTexture;
uniform sampler3D Noise3DTexture;
uniform float time;

vec4 randomIteration(vec4 seed)
{
   vec4 adder = vec4(0.735, 0.369, 0.438, 0.921);
   vec4 mult = vec4(94.34, 72.15, 59.372, 49.56);
   return fract((seed.zxwy + adder) * mult);
}

/* period 1, minimum at 0, output 0..1, probably does not work for neagtive values */
vec3 sawtooth(vec3 x)
{
    return 2.0 * min(fract(x), 1.0 - fract(x));
}

float brightness(vec3 color)
{
    return dot(color, vec3(0.3, 0.55, 0.15));
}

/* brightness is the only thing that may have values > 1.0 */
vec3 HSB2RGB(vec3 hsb)
{
    vec3 rgb;
    vec3 gray;

    /* Calculation for hue (vectorize!) */
    rgb = max(vec3(1.0) - 3.0/2.0 * sawtooth(vec3(hsb.r) + vec3(0.0, 1.0/3.0, 2.0/3.0)), vec3(0.0));
    
    /* normalize brightness */
    gray = vec3(1.0);
    gray /= brightness(gray);
    rgb /= brightness(rgb);
    
    /* saturation */
    rgb = mix(gray, rgb, hsb.g);
    
    /* brightness */
    rgb *= hsb.b;
    
    return rgb;
}

void main(void)
{
    // Real location in 3D space
	vec3 position = vec3(ppos.x / ppos.z, ppos.y / ppos.z, 1.0 / ppos.z);
	position.x *= 16.0 / 9.0;
	vec2 tex_coord = gl_TexCoord[0].xy / ppos.z;

    float fTime0_X = time;
	float bauchigkeit = 3.0;
	float lineStrength = 1.0;  // Really?
	float colorVariation = 0.5;
	vec3 mainColorHSB = vec3(0.5, 0.5, 0.5);
	mainColorHSB.b = 1.0; // Always draw at full brightness, no matter what
	float highlightAmount = 1.0;  //Really?
	float hermannBrightness = 0.5;  // This is on...
	float textureVignette = 0.5;

	/* Color changes over time, the speed depends on the speed of the fTime0_X update */
	mainColorHSB.r = fract(mainColorHSB.r + fTime0_X * 0.01);
    
    /* distance of best and second best center */
    float nearestDist = 1.0e20;
    float secondDist = 1.0e20;
    vec3 bestCenter = vec3(0.0); /* Position of the best center */
    vec2 bestMover = vec2(0.0);  /* move based on center */

    /* Calculate distances to randomly moving centers */
    vec4 seed = vec4(0.3, 0.2, 0.1, 0.9);
    vec3 baseColor = vec3(0.0);
    for (int i = 0; i < numCenters; i++)
    {
        /* Calculate position of the center */
		vec3 center = centers[i];
        seed = randomIteration(seed);
        
        /* Get the distance to the center */
        float curDist = length(position - center);
        if (curDist < secondDist)
        {
            if (curDist < nearestDist)
            {
                secondDist = nearestDist;
                nearestDist = curDist;
                bestCenter = center;
                bestMover = seed.yz;
                vec3 internalHSB = mainColorHSB + colorVariation *
                    (seed.rgb - 0.5) * vec3(1.0, 0.0, 0.0);
                internalHSB.r = fract(internalHSB.r + 0.5); /* + 0.5 so that I get inverted colors with the border) */
                internalHSB.g = clamp(internalHSB.g, 0.0, 1.0);
                baseColor = HSB2RGB(internalHSB);
            }
            else
            {
                secondDist = curDist;
            }
        }
    }
    
    /* Calculate the relative distance to the nearest point */
    /* What we get here is a float between 0.0 and 1.0 */
    float relDist = 2.0 * nearestDist / (nearestDist + secondDist + 0.0001);
    
    /* non-linear things */
    relDist = pow(relDist, bauchigkeit);
    
    /* Here I need the base color instead */
    vec3 bgColor = mix(HSB2RGB(mainColorHSB), vec3(0.0), lineStrength);
        
    gl_FragColor = vec4(mix(baseColor, bgColor, relDist), 1.0);
    
    /* background picture */
	//vec2 texPos = tex_coord + bestMover * (1.0 - relDist);
	vec2 texPos = 0.5 + 0.1 * vec2(1.0, -1.0) * position.xy + bestMover * (1.0 - relDist);
    vec4 tex = texture2D(BGTexture, texPos);
	float texDist = length(texPos - vec2(0.5));
	tex /= pow(textureVignette*texDist*3.0, 25.0) + 1.0; 
	gl_FragColor.xyz *= tex.rgb;

    /* Some highlights due to lighting */
    vec3 normal3D = normalize(position - bestCenter);
    //float jumpDist = floor(relDist*10.)/8.0;
    //vec3 normal3D = vec3(normal2D * sqrt(jumpDist), sqrt(1.0 - jumpDist));
    vec3 lightsource = normalize(vec3(0.4, 0.6, 0.3));
    float lighting = highlightAmount * (pow(max(0., abs(dot(normal3D, lightsource)) - 0.2), 3.0));
    gl_FragColor += vec4(vec3(lighting), 0.0);

	gl_FragColor *= hermannBrightness;

	// global vignette
	float vignette = smoothstep(1.0, 0.5, length(ppos.xy));
	gl_FragColor.rgb *= vignette;
}